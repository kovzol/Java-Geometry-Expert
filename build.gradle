plugins {
    id 'application'
}

tasks.withType(JavaCompile).configureEach {
    // The original sources of JGEX use the keyword "var" which is a reserved word in later Java versions.
    // As a workaround, we set compatibility to source = 8
    sourceCompatibility = 1.8
}

task msgFmt(type: Exec) {
    // TODO: Skip generating the translation if a new .class file is already present.
    description "Creates the .class files from the .po translations."
    group "build"
    ext.poDir = 'src/main/po'
    ext.classesDir = 'build/classes/java/main'
    ext.languages = 'de fa hu it pt rs zh_CN'
    commandLine "bash", "-c",  "for i in ${languages}; do " +
            "msgfmt --java2 -d ${classesDir} -r i18n.Messages -l \$i ${poDir}/\$i.po; done"
}

group = 'io.github.kovzol'
version = '0.80'

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'lib', include: ['*.jar'])
}

// It seems, both settings are required, one for running inside IntelliJ IDEA...
application {
    mainClass = 'wprover.GExpert'
}

// ...and one for the packaging:
jar {
    manifest {
        attributes(
                'Main-Class': 'wprover.GExpert'
        )
    }
}
