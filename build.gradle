/*
 * To run JGEX, just use
 *
 * ./gradlew run
 *
 * This is how you should create installer packages for JGEX:
 *
 * ./gradlew package      # build and package the software via the javapackager plugin
 * ./gradlew distZip      # build and package the software as a .zip via the application plugin
 *
 * Prerequisites: You need the msgfmt utility (in your PATH) to have internationalized texts.
 * (It is part of the gettext system.)
 */

import io.github.fvarrui.javapackager.model.FileAssociation

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.github.fvarrui:javapackager:1.7.5'
    }
}

plugins {
    id 'application'
    id 'java'
    id 'org.docstr.gwt' version '2.1.7'
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

// Global settings:
group = 'io.github.kovzol'
ext.softwareVersion = '0.86'

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/main/gwt']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

// These are default settings based on Gradle's standard layout:
ext.poDir = 'src/main/po'
ext.classesDir = 'build/classes/java/main'
ext.applicationMainClass = 'wprover.GExpert'
version = softwareVersion

tasks.addRule("Pattern: msgFmt_<FILE>Po: Compile <FILE>.po into <FILE>.class.") { String taskName ->
    if (taskName.startsWith('msgFmt_') && taskName.endsWith('Po')) {
        def language = (taskName - 'Po').substring('msgFmt_'.length())
        task(taskName) {
            def input = "${poDir}/${language}.po"
            inputs.file input
            def output = "${classesDir}/i18n/Messages_${language}.class"
            outputs.file output
            doLast {
                def msgFmtCmd = "msgfmt --java2 -d ${classesDir} -r i18n.Messages -l ${language} ${input}"
                try {
                    exec {
                        ignoreExitValue true
                        if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
                            commandLine 'cmd', '/c', msgFmtCmd
                        } else {
                            commandLine 'bash', '-c', msgFmtCmd
                        }
                    }
                } catch (Exception e) {
                    // If msgfmt is not available, log a warning but don't fail the build
                    logger.warn("Warning: msgfmt command not found. Internationalization will not be available.")
                    logger.warn("Install gettext to enable internationalization support.")
                    // Create an empty directory to satisfy the output requirement
                    def outputDir = new File("${classesDir}/i18n")
                    outputDir.mkdirs()
                }
            }
        }
    }
}

tasks.register("msgFmtAll") {
    description "Creates the .class files from the .po translations."
    group "build"
    def list = []
    FileTree files = fileTree(dir: poDir)
    files.visit { f ->
        if (f.name.endsWith('.po')) {
            def msgFmtAllTask = 'msgFmt_' + f.name - '.po' + 'Po'
            list << msgFmtAllTask
        }
    }
    dependsOn list
}
tasks.named("classes") { finalizedBy("msgFmtAll") }

tasks.addRule("Pattern: msgMerge_<FILE>Po: Merge the file keys.pot into <FILE>.po.") { String taskName ->
    if (taskName.startsWith('msgMerge_') && taskName.endsWith('Po')) {
        def language = (taskName - 'Po').substring('msgMerge_'.length())
        task(taskName) {
            def input = "${poDir}/keys.pot"
            inputs.file input
            def output = "${poDir}/${language}.po"
            outputs.file output
            doLast {
                def msgFmtCmd = "msgmerge -U ${output} ${input}"
                exec {
                    ignoreExitValue true
                    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
                        commandLine 'cmd', '/c', msgFmtCmd
                    } else {
                        commandLine 'bash', '-c', msgFmtCmd
                    }
                }
            }
        }
    }
}

tasks.register("msgMergeAll") {
    description "Updates the .po files from the .pot template."
    group "translation"
    def list = []
    FileTree files = fileTree(dir: poDir)
    files.visit { f ->
        if (f.name.endsWith('.po')) {
            def msgMergeAllTask = 'msgMerge_' + f.name - '.po' + 'Po'
            list << msgMergeAllTask
        }
    }
    dependsOn list
}


distributions {
    main {
        distributionBaseName = 'jgex'
        contents {
            into("bin") { from 'src/docs'}
        }
    }
}


repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.xmlgraphics:batik-anim:1.18',
            'org.apache.xmlgraphics:batik-awt-util:1.18',
            'org.apache.xmlgraphics:batik-bridge:1.18',
            'org.apache.xmlgraphics:batik-constants:1.18',
            'org.apache.xmlgraphics:batik-css:1.18',
            'org.apache.xmlgraphics:batik-dom:1.18',
            'org.apache.xmlgraphics:batik-ext:1.18',
            'org.apache.xmlgraphics:batik-gui-util:1.18',
            'org.apache.xmlgraphics:batik-gvt:1.18',
            'org.apache.xmlgraphics:batik-i18n:1.18',
            'org.apache.xmlgraphics:batik-parser:1.18',
            'org.apache.xmlgraphics:batik-script:1.18',
            'org.apache.xmlgraphics:batik-svg-dom:1.18',
            'org.apache.xmlgraphics:batik-swing:1.18',
            'org.apache.xmlgraphics:batik-util:1.18',
            'org.apache.xmlgraphics:batik-xml:1.18',
            'org.apache.xmlgraphics:xmlgraphics-commons:2.9',
            'com.googlecode.gettext-commons:gettext-commons:0.9.8',
            'org.graphper:graph-support:1.4.0',
            'ch.qos.logback:logback-core:1.2.9',
            'org.slf4j:slf4j-simple:2.0.7',
            'xml-apis:xml-apis:1.4.01',
            'xml-apis:xml-apis-ext:1.3.04',
            'commons-cli:commons-cli:1.4'
    compileOnly group: 'org.gwtproject', name: 'gwt-dev', version: '2.12.2' // has vulnerabilities even in the latest version
    compileOnly group: 'org.gwtproject', name: 'gwt-user', version: '2.12.2'

    // JUnit 5 dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

// GWT configuration
gwt {
    modules = ["webapp.JGEXWebApp"]
}

application {
    mainClass = applicationMainClass
}

jar {
    manifest {
        attributes(
                'Main-Class': application.mainClass
        )
    }
    mustRunAfter "msgFmtAll"
}

run {
    mustRunAfter "msgFmtAll"
    File runningDir = new File('src/docs')
    workingDir = runningDir
}

javadoc {
    mustRunAfter "msgFmtAll"
}

// Configure test task
test {
    useJUnitPlatform()  // Use JUnit 5 platform
    mustRunAfter "msgFmtAll"
}

// Make compileTestJava depend on msgFmtAll
tasks.named("compileTestJava") {
    dependsOn "msgFmtAll"
}

javapackager {
    description = "JGEX combines dynamic geometry software, automated geometry theorem prover with a visually dynamic approach for presenting proofs."
    mainClass = applicationMainClass
    bundleJre = true
    additionalResources = [ file ("src/docs/examples"), file ("src/docs/help"), file("src/docs/import"), file("src/docs/rules") ]
    organizationName = "JGEX Contributors"
    url = "https://github.com/kovzol/Java-Geometry-Expert"
    organizationEmail = "jgex@googlegroups.com"
    version = softwareVersion
    fileAssociations = [
        new FileAssociation(mimeType: 'application/vnd.geometry-expert', extension: 'gex', description: 'Geometry Expert File')
    ]


    winConfig.generateMsi = false
    winConfig.setupLanguages.english = "compiler:Default.isl"
    // winConfig.setupLanguages.spanish = "compiler:Languages\\Spanish.isl" // TODO: Create the Spanish translation.
    winConfig.setupLanguages.german = "compiler:Languages\\German.isl"
    winConfig.setupLanguages.italian = "compiler:Languages\\Italian.isl"
    winConfig.setupLanguages.portuguese = "compiler:Languages\\Portuguese.isl"
    // See https://github.com/HeliumProject/InnoSetup/tree/master/Languages
    winConfig.disableFinishedPage = false
    winConfig.disableRunAfterInstall = false

    linuxConfig.categories = ["Education", "Graphics", "Science"]
}

/*
 * The following tasks are tests for the GProver. Seemingly, there are
 * a couple of unhandled exceptions in the original codebase that are silently
 * catched with error messages for now, but all of this should be checked and fixed.
 *
 * The first task is a command line based test, and the second one allows
 * to select a file in a file chooser window, however, there is no report on success.
 * TODO.
 */

task runGproverMain(type: JavaExec) {
    description "A command line based test for the GProver, checking all input examples."
    mustRunAfter "msgFmtAll"
    classpath = sourceSets.main.runtimeClasspath
    File runningDir = new File('src/docs')
    workingDir = runningDir
    mainClass = 'gprover.Main'
}

task runGproverMain2(type: JavaExec) {
    description "A test for the GProver that allows to select a .gex file for checking."
    mustRunAfter "msgFmtAll"
    classpath = sourceSets.main.runtimeClasspath
    File runningDir = new File('src/docs')
    workingDir = runningDir
    mainClass = 'gprover.Main2'
}

// GWT-related tasks

// This copies files from the webapp folder to the war folder to be usable by GWT
tasks.register("prepareWebApp", Copy) {
    description "Copies web resources to the GWT war directory"
    group "GWT"
    from("src/main/webapp")
    into("build/gwt/war")
}

// Make GWT tasks depend on prepareWebApp
tasks.named("gwtDevMode") {
    dependsOn prepareWebApp

    // Configure Java compatibility for GWT development mode
    doFirst {
        // Ensure we're using a compatible Java version
        println "Running GWT DevMode with Java version: ${System.getProperty('java.version')}"
    }

    // Set JVM args for Java 17 compatibility
    jvmArgs = ["-Xmx1024M", "-Xms512M", "--add-opens=java.base/java.lang=ALL-UNNAMED", "--add-opens=java.base/java.util=ALL-UNNAMED", "--add-opens=java.base/java.nio=ALL-UNNAMED"]

    // Set GWT compiler args
    args = ["-style", "PRETTY", "-logLevel", "INFO"]
}

tasks.named("gwtCompile") {
    dependsOn prepareWebApp
}

// Add a task to run the GWT application in development mode
tasks.register("runGwtDev") {
    description "Runs the GWT application in development mode"
    group "GWT"
    dependsOn gwtDevMode
    doLast {
        println "GWT development server started at http://127.0.0.1:8888/index.html"
        println "Press Ctrl+C to stop the server"
    }
}

// Add a task to build and run the GWT application in production mode
tasks.register("buildGwt") {
    description "Builds the GWT application for production"
    group "GWT"
    dependsOn gwtCompile
    doLast {
        println "GWT application built successfully in build/gwt/war"
        println "You can deploy the contents of this directory to a web server"
    }
}

// Add a task to serve the compiled GWT application
tasks.register("serveGwt") {
    description "Serves the compiled GWT application"
    group "GWT"
    dependsOn buildGwt
    doLast {
        println "To serve the GWT application, you can use a simple HTTP server:"
        println "cd build/gwt/war"
        println "python -m http.server 8000"
        println "Then open http://localhost:8000 in your browser"
    }
}
